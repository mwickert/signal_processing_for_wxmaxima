/* Useful Digital Communications Functions for wxmaxima

MIT License

Copyright (c) 2025 Mark Wickert

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE. */


load ("distrib");
load("DSPTools.mac");

NRZ_bits(N_bits,Ns,pulse_shape,pulse_alpha,M):=block([x_bits,pulse_coeff,eps_scale],
    if pulse_shape = [] then pulse_shape:"rect",
    if pulse_alpha = [] then pulse_alpha:0.25,
    if M = [] then M:6,
    eps_scale:1,
    x_bits:2*random_bernoulli (1/2,N_bits)-1, /* data = equally likely +/- 1 bits */
    if pulse_shape = "rect" then pulse_coef:makelist(1,k,1,Ns)
    else if pulse_shape = "rrc" then pulse_coef:float(sqrt_rc_imp(Ns,pulse_alpha,M,eps_scale))
    else if pulse_shape = "rc" then pulse_coef:float(rc_imp(Ns,pulse_alpha,M)),
    y_bits:fir_filt(pulse_coef,upsample(x_bits,Ns))
);


cpx_awgn(x,Es2N0_dB,Ns):=block([N_samps,std_val],
    N_samps:length(x),
    std_val:float(sqrt(Ns*var(x)*10^(-Es2N0_dB/10)/2)),
    print("Standard Deviation: ",std_val),
    w:(random_normal(0, std_val, N_samps) + 
            %i * random_normal(0, std_val, N_samps)),
    y:x + w
);


eye_plot(x_bits,span,trim_head):=block([x_bits_trim,eye_points,eye_points2],
    if trim_head = [] then trim_head:0,
    N_segments:floor((length(x_bits)-trim_head)/span),
    x_bits_trim:makelist(x_bits[k+trim_head],k,1,N_segments*span),
    print("Slices Plotted: ",N_segments),
    eye_points:makelist(points(makelist(float(x_bits_trim[i+span*(j-1)]),
                        i,1,span)),j,1,N_segments),
    points_options:[grid=true,points_joined=true,color=blue,
                    point_type=none,xlabel="Samples",
                    ylabel="Amplitude",
                    title="Eye Plot"],
    eye_points2:append(points_options,eye_points),
    apply(wxdraw2d,eye_points2)
);


constellation(z,Ns,N_timing,trim_head,xy_lim):= block([z_trim,z_samps,N_samps],
    if trim_head = [] then z_trim:z
    else z_trim:makelist(z[k+trim_head],k,1,length(z)-trim_head),
    if xy_lim = [] then xy_lim:1.25,
    z_samps:downsample(z_trim,Ns,N_timing),
    N_samps:length(z_samps),
    z_re_im:makelist([realpart(z_samps[n]),imagpart(z_samps[n])],n,1,N_samps),
    wxdraw2d(
        proportional_axes=xy,
        nticks = 60,
        point_size=0.5,color=blue,point_type=filled_circle,
        points(z_re_im),
        grid=[1,1],
        xlabel = "In-phase",
        ylabel = "Quadrature",
        title = "Constellation Plot",
        xrange=[-xy_lim,xy_lim],
        yrange=[-xy_lim,xy_lim] 
    )
);


sqrt_rc_imp(Ns,alpha,M,eps_scale):= block([n,a,b_coef],
    if M = [] then M:6,
    if eps_scale = [] then eps_scale:1,
    a:alpha,
    /* Design the filter */
    n:makelist(k,k,-M*Ns,M*Ns),
    b_coeff:makelist(
        if abs(1 - 16*a^2*(n[k]/Ns)^2) <= 2.220446049250313e-16/eps_scale 
            then 1/2*((1+a)*sin((1+a)*%pi/(4*a))-(1-a)*cos((1-a)*%pi/(4*a))+(4*a)/%pi*sin((1-a)*%pi/(4*a)))
        else 4*a/(%pi*(1 - 16*a^2*(n[k]/Ns)^2))*(cos((1+a)*%pi*n[k]/Ns) + sinc((1-a)*n[k]/Ns)*(1-a)*%pi/(4*a)),
        k,1,length(n)
    )
);


rc_imp(Ns,alpha,M):=block([n,a,b_coef],
    if M = [] then M:6,
    a:alpha,
    /* Design the filter */
    n:makelist(k,k,-M*Ns,M*Ns),
    b_coeff:makelist(
        if (1 - 4*(a*n[k]/Ns)^2) = 0.0 then  %pi/4*sinc(1/(2.*a))
        else sinc(n[k]/Ns)*cos(%pi*a*n[k]/Ns)/(1 - 4*(a*n[k]/Ns)^2),
        k,1,length(n)
    )
);


sinc(x):= if x = 0 then 1 else sin(%pi*x)/(%pi*x);

/* End of CommTools.mac */